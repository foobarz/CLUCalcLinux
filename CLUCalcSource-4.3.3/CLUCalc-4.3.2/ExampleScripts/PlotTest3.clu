DefVarsN3();

// Functions
f1 = { x = _P(1); VecN3(x,_P(2)*x + _P(3)*x*x + _P(4)*x*x*x, 0.1) }
f2 = { x = _P(1); VecN3(0.1,x,0) }
f3 = { x = _P(1); VecN3(0,0.1,x) }

a = Slider("a", -1, 1, 0.05, 0);
b = Slider("b", -1, 1, 0.05, 1);
c = Slider("c", -1, 1, 0.05, 0);

// Plot the functions separately
:Red;	
:Plot(f1(x, a, b, c), [x, -1, 1, 10]);

:Green;
:Plot(f2(x), [x, -1, 1, 10]);

:Blue;
:Plot(f3(x), [x, -1, 1, 10]);

// Now plot the surface over the circles
// generated by the three points.
// Also add a color function.
:Plot(
	[f1(x, a, b, c)^f2(x)^f3(x), 
		Color(abs(x), 1-abs(x*x), 1-abs(x))],
	[x, -1, 1, 10]);


if (ExecMode & EM_CHANGE || ExecMode & EM_LINK)
{
	// Now create a point surface of different functions
	// This only has to evaluated once, when the script
	// is loaded. If the user later changes some sliders
	// we will simply redraw the vertex list generated
	// by Plot.
	Waves = Plot(
		[VecE3(y, 0.5*sin(2*Pi/4*x)*cos(2*Pi/4*y)-2, x),
			Color(cos(2*Pi/2*x*y), sin(2*Pi/2*x*y), 0.5*Ran())],
		[x, -2, 2, 20], [y, -2, 2, 20]);
}

// Draw the point surface here
:Waves;
