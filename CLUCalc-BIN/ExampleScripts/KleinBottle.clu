_FrameBoxSize = 0;
_BGColor = Black;
DefVarsN3();

if (ExecMode & EM_CHANGE)
{
fx = 1;
fy = 2.5;
fz = 1;
fr = 0.5;
v = 0;

Func =
{
	_u = _P[1];
	_v = _P[2];

	_x = fx*cos(_u) * (1 + sin(_u));
	_y = fy*sin(_u);
	_rad = fr* (1 - 0.5*cos(_u));
	_z = fz*_rad*sin(_v);

	VecE3(_x, _y, _z)
}

:Red;
:CenterLine = Plot(Func(u, v), [u, 0, 2*Pi, 20]);

u = 0;//2*Pi - 8*Pi/10;
i = 0;
:Cyan;
loop
{
	if (u > 2*Pi)
		break;

	x = fx*cos(u) * (1 + sin(u));
	y = fy*sin(u);
	rad = fr*(1 - 0.5*cos(u));
	z = fz*rad * sin(v);

	dx = -fx*sin(u) * (1 + sin(u)) + fx*cos(u) * cos(u);
	dy = fy*cos(u);
	drad = fr*sin(u);
	dz = fz*drad * sin(v);
	s2u = sin(u/2 + Pi/4);
	if (s2u < 0)
	{
		dx = -dx;
		dy = -dy;
	}

	A = VecN3(x - 0.5*dx, y - 0.5*dy, z - 0.5*dz);
	B = VecN3(x + 0.5*dx, y + 0.5*dy, z + 0.5*dz);
	P = *(A - B);
	S = SphereN3(x, y, z, rad);
	C = *S . P;
	
	colrad = (rad / fr) - 0.5;
	if (i == 0)
	{
		C_list = C;
		col_list = Color(0.1, colrad, colrad);
	}
	else
	{
		C_list << C;
		col_list << Color(0.1, colrad, colrad);
	}

	u = u + Pi/21;
	i = i + 1;
} 

// Uncomment the following line to also draw the circles.
//:C_list;

KleinBottle = DrawCircleSurface(C_list, col_list, 1);
//i = 0;
//SetFrameOrigin(0,0,5);
}
else
{
/*
	_DoAnimate = 1;
	?count = Len(CenterLine);

	?i = i + 1;
	if (i > count)
		i = 1;
	
	TranslateFrame(-CenterLine[i][1],-CenterLine[i][2],-CenterLine[i][3]);
*/
	:Cyan;
	// Uncomment the following line to also draw the circles.
	//:C_list;

	:KleinBottle;

	:Red;
	:CenterLine;

}
